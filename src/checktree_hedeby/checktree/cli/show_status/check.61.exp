#!/vol2/TCL_TK/glinux/bin/expect
# ___INFO__MARK_BEGIN__
##########################################################################
#
#  The Contents of this file are made available subject to the terms of
#  the Sun Industry Standards Source License Version 1.2
#
#  Sun Microsystems Inc., March, 2001
#
#
#  Sun Industry Standards Source License Version 1.2
#  =================================================
#  The contents of this file are subject to the Sun Industry Standards
#  Source License Version 1.2 (the "License"); You may not use this file
#  except in compliance with the License. You may obtain a copy of the
#  License at http://gridengine.sunsource.net/Gridengine_SISSL_license.html
#
#  Software provided under this License is provided on an "AS IS" basis,
#  WITHOUT WARRANTY OF ANY KIND, EITHER EXPRESSED OR IMPLIED, INCLUDING,
#  WITHOUT LIMITATION, WARRANTIES THAT THE SOFTWARE IS FREE OF DEFECTS,
#  MERCHANTABLE, FIT FOR A PARTICULAR PURPOSE, OR NON-INFRINGING.
#  See the License for the specific provisions governing your rights and
#  obligations concerning the Software.
#
#  The Initial Developer of the Original Code is: Sun Microsystems, Inc.
#
#  Copyright: 2006 by Sun Microsystems, Inc
#
#  All Rights Reserved.
#
##########################################################################
# ___INFO__MARK_END__


# source version dependent parts of the installation
global CHECK_ACTUAL_TEST_PATH CHECK_TESTSUITE_INSTALL_MODE
global check_name check_description check_needs check_functions
global check_root_access_needs check_category      
global check_use_installed_system check_init_level_procedure

# This file shows some check spezific things
# ---START OF TEST INFO SECTION-------------------------------------------------------------------------------
# "no" - we don't need root acess / "yes" - we need root access:
set check_root_access_needs    "no"              

# name of the test (best practice: use the name of the test directory)
set check_name                 "show_status"

# specifiy the test categories
# (VERIFIED if the test is QA inspected)
# other possible categories: COMPATIBILITY CSP INSTALL L10N MODULE PERFORMANCE SYSTEM VERIFIED
set check_category             "SYSTEM VERIFIED" 

# define the highest check level
set check_highest_level        0

# a sort check description for each runlevel:
# (runlevel 0 is mandatory)
set check_description(0)       "check all sdmadm show_status options" ;# runlevel 0
# set check_description(1)       "SOME OTHER DESCRIPTION"  ;# runlevel 1

# specify check dependencies
# (name all tests which have to run sucessfull before this test can run)
set check_needs                "hedeby_install"                  

# The name of the init level procedure for this test. The procedure is defined
# in the test file. 
set check_init_level_procedure "show_status_init_level"

# define tests setup and cleanup function. The functions are called for each
# runlevel. Before testsuite is starting with calling all check_functions
# the setup function is called. After finishing all check_functions the 
# cleanup function is called.
#
# The aim of the setup/cleanup functions is that the test should restore
# all modifications, even when the test failes completely.
set check_setup_function "show_status_setup"
set check_cleanup_function "show_status_cleanup"


# The check_functions array tells the testsuite which check functions should
# be called (and in which order)
# All check_functions should be defined at least in a file (*.tcl) in the
# check directory.
set check_functions            ""
lappend check_functions "show_status_check"


# ---END OF TEST INFO SECTION-------------------------------------------------------------------------------


# ---------------------------------------------------------------------------------
# here the tests begin ....
global show_status_current_cluster_config

# (the init level procedure defines which runlevels are supported)
# short  (   0 min - 15 min / run level   0 -  99 )"
# medium (  16 min -  1 h   / run level 100 - 199 )"
# long   (   1 h   -  4 h   / run level 200 - 299 )"
# day    ( > 4 h   - 24 h   / run level 300 - 399 )"
# week   ( >24 h            / run level 400 - 499 )"
proc show_status_init_level {} {
  global CHECK_ACT_LEVEL

  # this test currently supports only runlevel "0"
  if { $CHECK_ACT_LEVEL == 0 } {
     return 0
  }
  return -1
}

# the setup function for this test is storing the current
# cluster config nr in a global variable.
proc show_status_setup {} {
   global show_status_current_cluster_config
   
   ts_log_fine "doing setup ..."
   set show_status_current_cluster_config [get_current_cluster_config_nr]
}

# the cleanup function for this test will restore the
# current cluster config nr from the global variable set 
# be the setup function.
proc show_status_cleanup {} {
   global show_status_current_cluster_config

   ts_log_fine "doing cleanup ..."

   set_current_cluster_config_nr $show_status_current_cluster_config
}


#****** cli/show_status/parse_show_component_output() **********************
#  NAME
#     parse_show_component_output() -- parse sdmadm show_status output
#
#  SYNOPSIS
#     parse_show_component_output { output_var {status_array "ss_out" } } 
#
#  FUNCTION
#     This procedure is used to parse the output of the sdmadm show_status
#     command and return the parsed values in the specified result array.
#
#  INPUTS
#     output_var               - output of the sdmadm show_status cli command
#     {status_array "ss_out" } - name of the array were the parsed information
#                                should be stored. 
#                                The array (default="ss_out") has the following
#                                settings:
#                                ss_out(HOSTNAME,COMPONENT_NAME,status)
#                                ss_out(HOSTNAME,COMPONENT_NAME,section)
#
#  RESULT
#     number of parsed rows or -1 if the output could not be parsed
#
#  EXAMPLE
#     
#   set component_count [parse_show_component_output output]
#   
#   for {set i 0} {$i < $component_count} {incr i} {
#      set host   $ss_out($i,host)
#      set jvm    $ss_out($i,jvm)
#      set comp   $ss_out($i,component)
#      set state  $ss_out($i,state)
#      set type   $ss_out($i,type)
#   }
#*******************************************************************************
proc parse_show_component_output { output_var {status_array "ss_out" } } {
   upvar $output_var out
   upvar $status_array ss

   set help [split $out "\n"]
   set line_count -1
   set col_count 0
   array set last_values {}
   
   set known_colums(host)  [create_bundle_string "ShowComponentStatusCliCommand.HostCol"]
   set known_colums(jvm)  [create_bundle_string "ShowComponentStatusCliCommand.JvmCol"]
   set known_colums(component)  [create_bundle_string "ShowComponentStatusCliCommand.NameCol"]
   set known_colums(state)  [create_bundle_string "ShowComponentStatusCliCommand.StateCol"]
   set known_colums(type)  [create_bundle_string "ShowComponentStatusCliCommand.TypeCol"]
   
   foreach line $help {
      debug_puts "Process line $line_count: \"$line\""
      if { [string first "Error:" $line] >= 0 } {
         return -1
      } elseif {$line_count < 0} {
         
         set line [string trim $line]
         foreach col_name [split $line " "] {
            if {[string length $col_name] > 0} {
               set real_col_name ""
               foreach known_col [array names known_colums] {
                  if { $known_colums($known_col) == $col_name } {
                     set real_col_name $known_col
                     break;
                  }
               }
               if {$real_col_name == ""} {
                  add_proc_error "parse_show_component_output" -1 "Found unknown column $col_name in output of \"sdmadm show_status\""
                  return -1
               }
               set col($col_count,name)  $real_col_name
               set col($col_count,start_index) [string first "$col_name" "$line"]
               incr col_count
            }
         }
         set last_col_index [expr $col_count - 1]
         for {set i 0} {$i < $last_col_index} {incr i} {
            set col($i,end_index) $col([expr $i + 1],start_index)
            incr col($i,end_index) -1
            debug_puts "col$i: $col($i,name) = $col($i,start_index) -> $col($i,end_index)"
         }
         # We do not known the index of the last col
         # -1 means that the last col cosumes the rest of the line
         set col($last_col_index,end_index) -1
         debug_puts "col$i: $col($last_col_index,name) = $col($last_col_index,start_index) -> $col($last_col_index,end_index)"
         set line_count 0
      } elseif { [string length $line] == 0 } {
         continue
      } elseif { [string first "-------" $line] >= 0 } {
         continue
      } else {
         for {set i 0} {$i < $col_count} {incr i} {
            set col_name $col($i,name)
            if { $col($i,end_index) < 0 } {
               set end_index [string length $line]
            } else {
               set end_index $col($i,end_index)
            }
            set tvalue [string range $line $col($i,start_index) $end_index]
            set tvalue [string trim $tvalue]
            if {[string length $tvalue] == 0} {
               set tvalue $last_values($col_name)
            } else {
               set last_values($col_name) $tvalue
            }
            set ss($line_count,$col_name) $tvalue
         }
         incr line_count
      }
   }
   return $line_count
}


# The test will call the sdmadm show_status command for every 
# hedeby managed host and on the master host.
# If the output does not show all components in the "started"
# status the test will fail.
proc show_status_check {} {
   global hedeby_config


   set ge_qmaster_hosts [get_hedeby_default_services service_names]

   set nr_of_expected_jvms 0
   set nr_of_expected_components 0
   # setup master host expectations ...
   set test_hosts $hedeby_config(hedeby_master_host)
   set expected_jvms($hedeby_config(hedeby_master_host)) "executor_vm rp_vm"
   set expected_components($hedeby_config(hedeby_master_host)) "ca executor resource_provider reporter spare_pool"
   set expected_component_status "STARTED"

   # setup managed host expectations ...
   foreach host [get_all_movable_resources] {
      lappend test_hosts $host
      if { [lsearch -exact $ge_qmaster_hosts $host] >= 0 } {
         ts_log_fine "expecting ge adapter on host \"$host\" in vm \"rp_vm\""
         set expected_jvms($host) "executor_vm rp_vm"
         set component_helper "executor $service_names(service,$host)"
         ts_log_fine "expecting components \"$component_helper\" on host \"$host\""
         set expected_components($host) "$component_helper"
      } else {
         ts_log_fine "expecting vm \"executor_vm\" on host \"$host\""
         set expected_jvms($host) "executor_vm"
         ts_log_fine "expecting component \"executor\" on host \"$host\""
         set expected_components($host) "executor"
      }
   }

   set nr_of_jvms 0
   set nr_of_components 0
   set error_text ""
   
   # we don't use the pref type - hedeby must find the system anyway
   set show_user [get_hedeby_admin_user]
   set show_host $hedeby_config(hedeby_master_host)
   set output [sdmadm_command $show_host $show_user "-s [get_hedeby_system_name] sc"]

   set component_count [parse_show_component_output output]
   
   for {set i 0} {$i < $component_count} {incr i} {
      set host [resolve_host $ss_out($i,host)]
      set jvm  $ss_out($i,jvm)
      set comp $ss_out($i,component)
      set state $ss_out($i,state)
      debug_puts "Found running component $comp in jvm $jvm on host $host, state is $state"
      if {[info exists found_jvms($host)] } {
         set jvms $found_jvms($host)
         set jvm_index [lsearch $jvms $jvm]
         if {$jvm_index < 0} {
            lappend found_jvms($host) $jvm
            incr nr_of_jvms
         }
      } else {
         set found_jvms($host) $jvm
         incr nr_of_jvms
      }
      if { $ss_out($i,state) == $expected_component_status } {
         if {[info exists found_comps($host)]} {
            set comps $found_comps($host)
            set comp_index [lsearch $comps $comp]
            if {$comp_index < 0} {
               lappend found_comps($host) $comp
               incr nr_of_components
            }
         } else {
            set found_comps($host) $comp
            incr nr_of_components
         }
      }
   }

   set expected_jvms_count 0
   set expected_components_count 0
   set tested_jvms {}
   set tested_components {}
   
   foreach host $test_hosts {
      foreach jvm $expected_jvms($host) {
         incr expected_jvms_count 1
         lappend tested_jvms "${host}(${jvm})"
         if {[info exists found_jvms($host)]  && [lsearch $found_jvms($host) $jvm] >= 0} {
            debug_puts "Found jvm $jvm host host $host"
         } else {
            append error_text "\nJVM $jvm did not start on host $host"
         }
      }
      foreach comp $expected_components($host) {
         incr expected_components_count 1
         lappend tested_components "${host}(${comp})"
         if {[info exists found_comps($host)] != 1 || [lsearch $found_comps($host) $comp] < 0} {
            append error_text "Component $comp did not start on host $host\n"
         }
      }
   }
   ts_log_fine "nr of jvms:          $nr_of_jvms"
   ts_log_fine "nr of components:    $nr_of_components"
   ts_log_fine "nr of status infos:  $component_count"
   ts_log_fine "expected jvms:       $expected_jvms_count"
   if { $nr_of_jvms != $expected_jvms_count } {
      append error_text "Expected JVM count (=$expected_jvms_count) doesn't match found JVM count (=$nr_of_jvms)\n"
      append error_text "Expected JVMS:\n"
      foreach elem $tested_jvms {
         append error_text "   $elem\n"
      }
   }

   ts_log_fine "expected components: $expected_components_count"
   if { $nr_of_components != $expected_components_count } {
      append error_text "Expected COMP count (=$expected_components_count) doesn't match found COMP count (=$nr_of_components)\n"
      append error_text "Expected COMPS:\n"
      foreach elem $tested_components {
         append error_text "   $elem\n"
      }
   }


   if { $error_text != "" } {
      append error_text "\n\n---debug info (start) -----------------------------------------\n"
      append error_text "\nsdmadm show_status output on host \"$hedeby_config(hedeby_master_host)\":\n"
      append error_text $output
      append error_text "\n"
      append error_text "---debug info (end) -------------------------------------------"
      add_proc_error "show_status_check" -1 $error_text
   }
}



