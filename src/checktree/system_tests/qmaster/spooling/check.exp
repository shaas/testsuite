#!/vol2/TCL_TK/glinux/bin/expect
#___INFO__MARK_BEGIN__
##########################################################################
#
#  The Contents of this file are made available subject to the terms of
#  the Sun Industry Standards Source License Version 1.2
#
#  Sun Microsystems Inc., March, 2001
#
#
#  Sun Industry Standards Source License Version 1.2
#  =================================================
#  The contents of this file are subject to the Sun Industry Standards
#  Source License Version 1.2 (the "License"); You may not use this file
#  except in compliance with the License. You may obtain a copy of the
#  License at http://gridengine.sunsource.net/Gridengine_SISSL_license.html
#
#  Software provided under this License is provided on an "AS IS" basis,
#  WITHOUT WARRANTY OF ANY KIND, EITHER EXPRESSED OR IMPLIED, INCLUDING,
#  WITHOUT LIMITATION, WARRANTIES THAT THE SOFTWARE IS FREE OF DEFECTS,
#  MERCHANTABLE, FIT FOR A PARTICULAR PURPOSE, OR NON-INFRINGING.
#  See the License for the specific provisions governing your rights and
#  obligations concerning the Software.
#
#  The Initial Developer of the Original Code is: Sun Microsystems, Inc.
#
#  Copyright: 2001 by Sun Microsystems, Inc.
#
#  All Rights Reserved.
#
##########################################################################
#___INFO__MARK_END__

# define global variable in this namespace
global check_name 
global check_category
global check_description 
global check_needs
global check_functions 
global check_highest_level
global check_init_level_procedure
global check_root_access_needs
global env

# set check_root_access_needs "yes"


# define a level initialization procedure:
set check_init_level_procedure "qmaster_spooling_init_level"

# define test's name and run level descriptions
set check_name "qmaster_spooling"
set check_root_access_needs  "yes"
set check_category "COMPATIBILITY SYSTEM L10N VERIFIED"
set check_highest_level 0
set check_description(0) "check qmaster spooling"

# define test's dependencies
set check_needs "init_core_system" 

# setup and cleanup functions
set check_setup_function "qmaster_spooling_setup"
set check_cleanup_function "qmaster_spooling_cleanup"

# define test's procedure order
set check_functions ""
lappend check_functions "qmaster_spooling_prj_restart"
lappend check_functions "qmaster_spooling_rqs_restart"
lappend check_functions "qmaster_spooling_ar_restart"
lappend check_functions "qmaster_spooling_issue2442"

proc qmaster_spooling_init_level {} {
   global CHECK_ACT_LEVEL

   switch -- $CHECK_ACT_LEVEL {
      "0" { 
         return 0
      }
   }

   return -1
}

# -------- local test procedures -----------------------------------------------

proc qmaster_spooling_setup {} {
}

proc qmaster_spooling_cleanup {} {
   global ts_config

   if {$ts_config(gridengine_version) >= 61} {
      if {[get_rqs_list rqs_names "" "" 0] == 0} {
         foreach elem $rqs_names {
            ts_log_fine "Removing resource quota sets $elem ...\n"
            del_rqs $elem
         }
      }
   }
   if {$ts_config(gridengine_version) >= 62} {
      delete_all_ars
   }
}

proc cmp_config { conf_a conf_b fields check} {
   upvar $conf_a a
   upvar $conf_b b

   # check all fields
   foreach field $fields {
      # error - field name doesn't exist
      if { ![info exists a($field)] } {
         ts_log_severe "$check: original object doesn't contain attribute $field"
      } else {
         # error - field name doesn't exist
         if { ![info exists b($field)] } {
            ts_log_severe "$check: reread object doesn't contain attribute $field"
         } else {
            # error - fields differ
            if { [string compare $a($field) $b($field)] != 0 } {
               ts_log_severe "$check: attribute field differs between original object:\n old ($a($field))\n new ($b($field))"
            }
         }
      }
   }
}

# check if spooled projects are still available after qmaster restart
proc qmaster_spooling_prj_restart {} {
   global ts_config

   if {$ts_config(product_type) != "sgeee"} {
      ts_log_config "this can only run in sgeee clusters"
   } else {
      set check_fields "name oticket fshare acl xacl"

      set PROJECT_NAME "spool_prj"
      set config(oticket) "123"
      set config(fshare)  "234"
      set config(acl)  "deadlineusers"
      set config(xacl)  "defaultdepartment"

      add_project $PROJECT_NAME config

      shutdown_and_restart_qmaster

      get_project $PROJECT_NAME new_config

      cmp_config config new_config $check_fields "qmaster_spooling_prj_restart"

      del_project $PROJECT_NAME
   }
}


# check if spooled rqs are still available after qmaster restart
proc qmaster_spooling_rqs_restart {} {
   global ts_config

   if {$ts_config(gridengine_version) >= 61} {
      set rqs_name "test_spooling"

      set rules ""
      lappend rules "users {*} to slots=10"
      lappend rules "hosts {*} to slots=10"
      lappend rules "queues {!*} to slots=10"
      lappend rules "hosts !* to slots=10,arch=sol-sparc64"
      lappend rules "pes pe1,pe2,pe3 to slots=10,arch=sol-sparc64"
      lappend rules "hosts {*} to slots=\$num_proc*2+\$num_proc*1"
      set rqs_charray($rqs_name,limit)   "$rules"
      set rqs_charray($rqs_name,enabled) "TRUE"
      set rqs_charray($rqs_name,description) "test description for spooling \?+-:"
      add_rqs rqs_charray

      shutdown_and_restart_qmaster

      get_rqs new_charray "$rqs_name"

      set check_fields [array names rqs_charray]
      cmp_config rqs_charray new_charray $check_fields "qmaster_spooling_rqs_restart"

      del_rqs "$rqs_name"
   }
}

proc qmaster_spooling_ar_restart {} {
   global ts_config
   global actual_host  
   global qinstance
   global CHECK_USER
   global CHECK_FIRST_FOREIGN_SYSTEM_USER
   global CHECK_SECOND_FOREIGN_SYSTEM_USER

   if {$ts_config(gridengine_version) >= 62} {
      set rqs_name "test_spooling"
      set actual_host [host_conf_get_suited_hosts]
      set qinstance [get_queue_instance "all.q" $actual_host]

      set now [clock seconds]
      set start  [clock format [expr $now + 20000] -format "%Y%m%d%H%M.%S"]
      set starto [clock format [expr $now + 20000] -format "%m/%d/%Y %H:%M:%S"]
      set end    [clock format [expr $now + 23661] -format "%Y%m%d%H%M.%S"]
      set endo   [clock format [expr $now + 23661] -format "%m/%d/%Y %H:%M:%S"]


      #  To be sure, no conflict exists
      delete_all_ars   

      set    args "-a $start "
      append args "-e $end "
      append args "-A test_ar_account "
      append args "-ckpt testcheckpointobject "
      append args "-he yes "
      append args "-l 'h_rt=200, h=${actual_host}' "
      append args "-m abe "
      append args "-masterq all.q "
      append args "-M $CHECK_FIRST_FOREIGN_SYSTEM_USER@sun.com,$CHECK_SECOND_FOREIGN_SYSTEM_USER@sun.com "
      append args "-N test_ar_name "
      append args "-w e "
      append args "-pe mytestpe 1- "
      append args "-q $qinstance "
      append args "-u '!$CHECK_FIRST_FOREIGN_SYSTEM_USER,$CHECK_SECOND_FOREIGN_SYSTEM_USER,!root' "

      ts_log_fine "qrsub $args\n"
      set ar_id [submit_ar "$args"]
      if {$ar_id < 0} {
       return
      }

      set check_values(id)                             "$ar_id"
      set check_values(name)                           "test_ar_name"
      set check_values(owner)                          "$CHECK_USER"  
      set check_values(checkpoint_name)                "testcheckpointobject"
      set check_values(start_time)                     "$starto"
      set check_values(end_time)                       "$endo"
      set check_values(duration)                       "01:01:01"
      # disabled because does not work on all architectures, sometimes it's wheel sometimes staff
      # set check_values(group)                          "staff"
      set check_values(account)                        "test_ar_account"
      set check_values(resource_list)                  "h_rt=200, hostname=${actual_host}"
      set check_values(error_handling)                 "true"
      set check_values(granted_slots_list)             "$qinstance=5"
      set check_values(granted_parallel_environment)   "mytestpe slots 1-9999999"
      set check_values(mail_options)                   "abe"
      set check_values(mail_list)                      "$CHECK_FIRST_FOREIGN_SYSTEM_USER@sun.com,$CHECK_SECOND_FOREIGN_SYSTEM_USER@sun.com"
      set check_values(acl_list)                       "$CHECK_SECOND_FOREIGN_SYSTEM_USER"
      set check_values(xacl_list)                      "$CHECK_FIRST_FOREIGN_SYSTEM_USER,root"


      ts_log_fine "qrstat -ar $ar_id\n"
      set ret [parse_qrstat_check $ar_id check_values]
      if {$ret != 0 } {
       return
      }

      shutdown_and_restart_qmaster

      ts_log_fine "check output of qrstat -ar $ar_id after spooling\n"
      set ret [parse_qrstat_check $ar_id check_values]
      if {$ret != 0 } {
       return
      }

      ts_log_fine "test qmaster_spooling_ar_restart OK"
   }
}

# test the issue 2442: incomplete error loging in case of classic spooling failures
proc qmaster_spooling_issue2442 {} {
   global ts_config ts_host_config CHECK_USER

   # test only for classic spooling
   if { [string compare "$ts_config(spooling_method)" "classic"] == 0 } {
      # get all the spooled objects - read them from the qmaster spooling directory
      set qmaster_spool_dir [get_qmaster_spool_dir]
      set objects {}
      # exclude the directories job_scripts, jobs, schedd and zombies
      foreach dir [get_dir_names "$qmaster_spool_dir"] {
         if {[string first "job" "$dir"] == -1 && [string first "zombies" "$dir"] == -1 && [string first "schedd" "$dir"] == -1} {
            lappend objects $dir
         }
      }
      # set the unique number which we use in the object name 
      # (just for case we run the test more times without reinstalling the cluster)
      set unq_num [expr {int(rand()*1000)}]
      # create wrong configuration files for each object
      foreach object $objects {
         # if the directory contains subdirectories, create object there (example: qinstances)
         set sub_dirs [get_dir_names "$qmaster_spool_dir/$object"]
         if {[string compare $sub_dirs ""] == 0} {
            start_remote_prog $ts_config(master_host) $CHECK_USER "echo" "\"wrong_argument\" >> $qmaster_spool_dir/$object/wrong_$object$unq_num"
         } else {
            foreach sub_dir $sub_dirs {
               start_remote_prog $ts_config(master_host) $CHECK_USER "echo" "\"wrong_argument\" >> $qmaster_spool_dir/$object/$sub_dir/wrong_$object$unq_num"
            }
         }
      }
      # now restart the cluster and check the qmaster messages
      shutdown_and_restart_qmaster
      set error 0
      set missing_log ""
      foreach object $objects {
         set sub_dirs [get_dir_names "$qmaster_spool_dir/$object"]
         if {[string compare $sub_dirs ""] == 0} {
            # messages are version dependend
            if { $ts_config(gridengine_version) >= 62 } {
               set msg "[translate_macro MSG_FLATFILE_ERROR_READINGFILE_S "$qmaster_spool_dir/$object/wrong_$object$unq_num"]"
            } else {
               set msg "[translate_macro MSG_CONFIG_READINGFILE_SS "$object" "wrong_$object$unq_num"]"
            }
            # look for the error message for the given object
            set result [start_remote_prog $ts_config(master_host) $CHECK_USER "cat" "$qmaster_spool_dir/messages | grep \\\"$msg\\\""]
            # remove the wrong configuration file
            start_remote_prog $ts_config(master_host) $CHECK_USER "rm" "$qmaster_spool_dir/$object/wrong_$object$unq_num"
         } else {
            foreach sub_dir $sub_dirs {
               # messages are version dependend
               if { $ts_config(gridengine_version) >= 62 } {
                  set msg "[translate_macro MSG_FLATFILE_ERROR_READINGFILE_S "$qmaster_spool_dir/$object/$sub_dir/wrong_$object$unq_num"]"
               } else {
                  set msg "[translate_macro MSG_CONFIG_READINGFILE_SS "$object/$sub_dir" "wrong_$object$unq_num"]"
               }
               # look for the error message for the given object
               set result [start_remote_prog $ts_config(master_host) $CHECK_USER "cat" "$qmaster_spool_dir/messages | grep \\\"$msg\\\""]
               # remove the wrong configuration file
               start_remote_prog $ts_config(master_host) $CHECK_USER "rm" "$qmaster_spool_dir/$object/$sub_dir/wrong_$object$unq_num"
            }
         }
         # check the result if the message was found
         if {[string compare $result ""] == 0} {
            set error 1
            lappend missing_log $object
         }
         
      }
      shutdown_and_restart_qmaster
      if {$error == 1} {
         ts_log_severe "Missing messages in qmaster log file for object(s): $missing_log"
         return -1
      } else {
         ts_log_fine "Successfully performed error logging test qmaster_spooling_issue2442."
      }
   } else {
      ts_log_fine "qmaster_spooling_issue2442 test skipped for $ts_config(spooling_method) spooling method"
   }
   return 0
}
