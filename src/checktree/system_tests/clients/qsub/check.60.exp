#!/vol2/TCL_TK/glinux/bin/expect
#___INFO__MARK_BEGIN__
##########################################################################
#
#  The Contents of this file are made available subject to the terms of
#  the Sun Industry Standards Source License Version 1.2
#
#  Sun Microsystems Inc., March, 2001
#
#
#  Sun Industry Standards Source License Version 1.2
#  =================================================
#  The contents of this file are subject to the Sun Industry Standards
#  Source License Version 1.2 (the "License"); You may not use this file
#  except in compliance with the License. You may obtain a copy of the
#  License at http://gridengine.sunsource.net/Gridengine_SISSL_license.html
#
#  Software provided under this License is provided on an "AS IS" basis,
#  WITHOUT WARRANTY OF ANY KIND, EITHER EXPRESSED OR IMPLIED, INCLUDING,
#  WITHOUT LIMITATION, WARRANTIES THAT THE SOFTWARE IS FREE OF DEFECTS,
#  MERCHANTABLE, FIT FOR A PARTICULAR PURPOSE, OR NON-INFRINGING.
#  See the License for the specific provisions governing your rights and
#  obligations concerning the Software.
#
#  The Initial Developer of the Original Code is: Sun Microsystems, Inc.
#
#  Copyright: 2001 by Sun Microsystems, Inc.
#
#  All Rights Reserved.
#
##########################################################################
#___INFO__MARK_END__

lappend check_functions "check_deadline" 
lappend check_functions "check_option_sync"
lappend check_functions "check_option_sync_t"
lappend check_functions "check_option_sync_ww"
lappend check_functions "check_option_terse"

proc check_qhold_jobname {} {
   global ts_config
   global CHECK_FIRST_FOREIGN_SYSTEM_USER
   global job_output_dir test_host

   # first clean up output directory:  
   setup_output_directory

   # submit jobs, second one depends on FIRST one
   set jobargs "-h -l h=${test_host} -N FIRST -o $job_output_dir -e $job_output_dir $ts_config(product_root)/examples/jobs/sleeper.sh 90" 
   set hold_job1 [submit_job $jobargs] 

   set jobargs "-hold_jid FIRST -l h=${test_host} -o $job_output_dir -e $job_output_dir $ts_config(product_root)/examples/jobs/sleeper.sh 30" 
   set hold_job2 [submit_job $jobargs] 

   # wait for jobs to get in pending state
   wait_for_jobpending $hold_job1 "FIRST" 200
   wait_for_jobpending $hold_job2 "Sleeper" 200 

   # job 1 should not run within the next 60 seconds  (user hold)
   if {[wait_for_jobstart $hold_job1 "Sleeper" 60 0] != -1} {
      ts_log_severe "job $hold_job1 should not run until yet(1)"
   }

   # release job 1
   release_job $hold_job1
   wait_for_jobstart $hold_job1 "FIRST" 100

   # job 2 should not run within the next minute (dependency)
   if {[wait_for_jobstart $hold_job2 "Sleeper" 60 0] != -1} {
      ts_log_severe "job $hold_job2 should not run until yet(2)"
   }

   # job1 will stop in ca. 30 sec
   wait_for_jobend $hold_job1 "FIRST" 100 0

   # job2 should run now
   wait_for_jobstart $hold_job2 "Sleeper" 100

   # job2 should run till end
   wait_for_jobend $hold_job2 "Sleeper" 100 0

   set back [del_job_files $hold_job1 $job_output_dir 2]
   if {$back != 2} {
      ts_log_severe "job $hold_job1 did not produce error and output file"
   }

   set back [ del_job_files $hold_job2 $job_output_dir 2 ] 
   if { $back != 2 } {
      ts_log_severe "job $hold_job2 did not produce error and output file"
   }

   ts_log_fine "Test wait for not existent job (job should immediately run)"
   set jobargs "-hold_jid FIRST -l h=${test_host} -o /dev/null -e /dev/null $ts_config(product_root)/examples/jobs/sleeper.sh 30"
   set job_id [submit_job $jobargs]
   wait_for_jobstart $job_id "Sleeper" 60
   wait_for_jobend $job_id "Sleeper" 60 0

   ts_log_fine "additional job submit with same name test"
   set jobargs "-h -l h=${test_host} -N FIRST -o /dev/null -e /dev/null $ts_config(product_root)/examples/jobs/sleeper.sh 30"
   set hold_job1 [submit_job $jobargs]

   ts_log_fine "additional job submit with same name test"
   set jobargs "-h -l h=${test_host} -N FIRST -o /dev/null -e /dev/null $ts_config(product_root)/examples/jobs/sleeper.sh 30"
   set hold_job2 [submit_job $jobargs]

   wait_for_jobpending $hold_job1 "FIRST" 60
   wait_for_jobpending $hold_job2 "FIRST" 60

   set jobargs "-hold_jid FIRST -l h=${test_host} -o /dev/null -e /dev/null $ts_config(product_root)/examples/jobs/sleeper.sh 30"
   set hold_job3 [submit_job $jobargs 0]
   if {$hold_job3 <= 0} {
      ts_log_severe "job $hold_job3 should depend on multiple jobs, selected by name"
   } else {
      ts_log_fine "submitted job with multiple predecessors selected by job name: $hold_job3 - correct"
   } 
    
   delete_job $hold_job1

   ts_log_fine "submitting hold job ..."
   set jobargs "-hold_jid FIRST -l h=${test_host} -o /dev/null -e /dev/null $ts_config(product_root)/examples/jobs/sleeper.sh 30" 
   set hold_job3 [submit_job $jobargs]

   ts_log_fine "submitting job with same name (FIRST) ..."
   set jobargs "-h -l h=${test_host} -N FIRST -o /dev/null -e /dev/null $ts_config(product_root)/examples/jobs/sleeper.sh 30"
   ts_log_fine $jobargs
   set hold_job1 [submit_job $jobargs 0]
   if {$hold_job1 <= 0} {
      ts_log_severe "job $hold_job1 should have been submitted - -N name is ok, even if job dependencies containing this name exist"
   } else {
      ts_log_fine "could submit job reusing jobname that is in use in a job dependency: $hold_job1 - correct"
   }

   delete_job $hold_job1
   delete_job $hold_job2
   delete_job $hold_job3

   # test foreign user influence ...
   ts_log_fine "checking hold for foreign user ..."
   ts_log_fine "submitting job with same name (FIRST) as user $CHECK_FIRST_FOREIGN_SYSTEM_USER ..."
   set jobargs "-h -l h=${test_host} -N FIRST -o /dev/null -e /dev/null $ts_config(product_root)/examples/jobs/sleeper.sh 30"
   ts_log_fine $jobargs
   set hold_job1 [submit_job $jobargs 1 30 "" $CHECK_FIRST_FOREIGN_SYSTEM_USER]

   ts_log_fine "submitting hold job ..."
   set jobargs "-hold_jid FIRST -l h=${test_host} -o /dev/null -e /dev/null $ts_config(product_root)/examples/jobs/sleeper.sh 30"
   set hold_job3 [submit_job $jobargs]

   wait_for_jobstart $hold_job3 "Sleeper" 75
   wait_for_jobend $hold_job3 "Sleeper" 75 0

   ts_log_fine "additional job submit with same name test"
   set jobargs "-h -l h=${test_host} -N FIRST -o /dev/null -e /dev/null $ts_config(product_root)/examples/jobs/sleeper.sh 30"
   set hold_job2 [submit_job $jobargs]

   ts_log_fine "submitting hold job ..."
   set jobargs "-hold_jid FIRST -l h=${test_host} -o /dev/null -e /dev/null $ts_config(product_root)/examples/jobs/sleeper.sh 30"
   set hold_job3 [submit_job $jobargs]

   if {[wait_for_jobstart $hold_job3 "Sleeper" 60 0] != -1} {
      ts_log_severe "job $hold_job3 should not run until yet(3)"
   }

   delete_job $hold_job3
   delete_job $hold_job2
 
   release_job $hold_job1
   wait_for_jobstart $hold_job1 "FIRST" 60
   wait_for_jobend $hold_job1 "FIRST" 60 0
}

#                                                             max. column:     |
#****** qsub/check_option_v() ******
# 
#  NAME
#     check_option_v -- ??? 
#
#  SYNOPSIS
#     check_option_v { } 
#
#  FUNCTION
#     ??? 
#
#  INPUTS
#
#  RESULT
#     ??? 
#
#  EXAMPLE
#     ??? 
#
#  NOTES
#     ??? 
#
#  BUGS
#     ??? 
#
#  SEE ALSO
#     ???/???
#*******************************
proc check_option_v {} {
   global ts_config
   global job_output_dir test_host

   # first clean up output directory:
   setup_output_directory

   set option "-v DISPLAY,test_var1=hello=world,test_var2=hello=my=friend"
   set jobargs "$option -l h=${test_host} -o $job_output_dir -e $job_output_dir $ts_config(product_root)/examples/jobs/sleeper.sh 5"

   set result [submit_job $jobargs 0]
   ts_log_fine "\njob $result has args: \"$jobargs\""

   if {$result < 0} {
      ts_log_severe "could not submit job"
      return
   }

   # check if the environment variables are set correctly in the job object
   set qstat_out [start_sge_bin qstat "-j"]
   if {[string match *DISPLAY*,test_var1=hello=world,test_var2=hello=my=friend* $qstat_out] == -1} {
      ts_log_severe "-v option failed, equal sign within variable values not supported"
   }

   # JG: TODO: instead of starting the example sleeper job, we should output the environment and sleep for some
   # time. Then we could check, if the passed environment variables are really set in the job environment.

   set time [expr [timestamp] + 300]
   while {[timestamp] < $time} {
      sleep 1
      set back [was_job_running $result 0]
      if {$back != -1} {
         set back [del_job_files $result $job_output_dir 2]
         if {$back != 2} {
            ts_log_severe "job $result did not produce error and output file\njobargs: $jobargs"
         }
         return
      }
   }
 
   ts_log_severe "timeout while waiting for job accounting"
}

#                                                             max. column:     |
#****** qsub/check_option_sync() ******
# 
#  NAME
#     check_option_sync -- ??? 
#
#  SYNOPSIS
#     check_option_sync { } 
#
#  FUNCTION
#     ??? 
#
#  INPUTS
#
#  RESULT
#     ??? 
#
#  EXAMPLE
#     ??? 
#
#  NOTES
#     ??? 
#
#  BUGS
#     ??? 
#
#  SEE ALSO
#     ???/???
#*******************************
proc check_option_sync {} {
   global ts_config
   global job_output_dir test_host

   # first clean up output directory:  
   setup_output_directory

   set option "-sync y"
   set jobargs "$option -o /dev/null -e /dev/null -j y $ts_config(testsuite_root_dir)/scripts/qsub_exit.sh 5" 
   set result [start_sge_bin "qsub" $jobargs]
   ts_log_fine "\njob has args: \"$jobargs\""
   ts_log_fine "exit code: $prg_exit_state"
   if {$prg_exit_state != 5} {
      ts_log_severe "qsub did not exit with correct exit code: $result"
   }
}

#                                                             max. column:     |
#****** qsub/check_option_sync_t() ******
# 
#  NAME
#     check_option_sync_t -- ??? 
#
#  SYNOPSIS
#     check_option_sync_t { } 
#
#  FUNCTION
#     ??? 
#
#  INPUTS
#
#  RESULT
#     ??? 
#
#  EXAMPLE
#     ??? 
#
#  NOTES
#     ??? 
#
#  BUGS
#     ??? 
#
#  SEE ALSO
#     ???/???
#*******************************
proc check_option_sync_t {} {
   global ts_config
   global job_output_dir test_host

   # first clean up output directory:  
   setup_output_directory

   set option "-sync y -t 1-2"
   set jobargs "$option -o /dev/null -e /dev/null -j y $ts_config(testsuite_root_dir)/scripts/qsub_exit.sh 5"
   set result [start_sge_bin "qsub" "$jobargs"]
   ts_log_fine "\njob $result has args: \"$jobargs\""
   ts_log_fine "exit state: $prg_exit_state"
   if {$prg_exit_state != 5} {
      ts_log_severe "qsub did not exit with correct exit code: $result"
   }
}

proc check_option_sync_ww {} {
   global ts_config
   global CHECK_USER

   set found_warning 0
   set found_job_submitted 0
   set found_timeout 0

   set JOB_SUBMITTED [translate_macro MSG_JOB_SUBMITJOB_US "*" "*"]
   set WARNING       [translate_macro MSG_JOB_VERIFYWARN]
   set NOSUITABLEQ_S [translate_macro MSG_JOB_NOSUITABLEQ_S $WARNING]

   set option "-sync y -w w -l arch=no_arch"
   set jobargs "$option -o /dev/null -e /dev/null $ts_config(product_root)/examples/jobs/sleeper.sh 100"

   set qsub_id [open_remote_spawn_process $ts_config(master_host) $CHECK_USER "qsub" "$jobargs"]
   set sp_id [lindex $qsub_id 1]
  
   set timeout 20
   expect {
      -i $sp_id full_buffer {
         ts_log_severe "buffer overflow please increment CHECK_EXPECT_MATCH_MAX_BUFFER value"
      }
       -i $sp_id -- "$NOSUITABLEQ_S" {
         set found_warning 1
         exp_continue
      }
      -i $sp_id -- "$JOB_SUBMITTED" {
         set found_job_submitted 1
         trigger_scheduling
         exp_continue
      }
      -i $sp_id "_exit_status_:(*)*\n" {
            ts_log_fine "got exit status from script:\n$expect_out(buffer)"
      }
      -i $sp_id timeout {
         set found_timeout 1
      }
   }
   close_spawn_process $qsub_id

   if {$found_warning == 0} {
      ts_log_severe "IZ 1945: qsub -sync -w w broken - got no warning message"
   } elseif {$found_job_submitted == 0} {
      ts_log_severe "IZ 1945: qsub -sync -w w broken - got no submitted message"
   } elseif {$found_timeout == 0} {
      ts_log_severe "IZ 1945: qsub -sync -w w broken - sync does not wait until jobend"
   }

   delete_all_jobs
   wait_for_end_of_all_jobs
}

proc invalid_job_has_valid_account_string {option} {
   set ret {}

   lappend ret "test\ntest"
   lappend ret "test\ttest"
   lappend ret "test\rtest"
   lappend ret "test/test"
   lappend ret "test:test"
   lappend ret "test@test"
   lappend ret "test\\test"
   lappend ret "test*test"
   lappend ret "test?test"

   if {$option == "-N"} {
      lappend ret "1test"
   }

   return $ret
}

proc valid_job_has_valid_account_string {option} {
   set ret {}

   lappend ret "test"
   lappend ret "test test"
   lappend ret "test!test"
   lappend ret "test,test"
   lappend ret "test;test"
   lappend ret "test.test"
   lappend ret "testäöüßtest"
   lappend ret "test=test"
   lappend ret "test%test"
   lappend ret "test|test"
   lappend ret "\"test\""

   if {$option == "-A"} {
      lappend ret "1test"
   }

   return $ret
}

#                                                             max. column:     |
#****** qsub/check_option_terse() ******
# 
#  NAME
#     check_option_terse -- Check to see if the terse option works
#
#  SYNOPSIS
#     check_option_terse { } 
#
#  FUNCTION
#     The function tries to submit a job with -terse option to see if it throws
#     only the job id in case of successfull job submission.
#     It further check to verify if the -terse is working by,
#     -  checks to see if the result of start_sge_bin throws the o/p starting
#       with "Your job", if yes then the option is not working.
#     -  checks to see if the result when passed to get_extended_job_info as
#       job-id gives positive result
#
#  INPUTS
#     NONE
#
#  RESULT
#     ??? 
#
#  EXAMPLE
#     qsub -terse sleeper.sh
#
#  NOTES
#     This is for RFE 6355875 
#     "-terse" that would have qsub just display the job number, and none of the
#     '[Yy]our job number...' stuff.
#
#     If there are errors, they should continue to be submitted via stderr.
#
#     Thus, the output of qsub would look like:
#
#     $ qsub -terse -cwd -t 1:5 ./runme.sh
#     1321772.1-5:1
#
#     $ qsub -terse -cwd ./runme.sh
#     1321775
#
#     If the user doesn't specify '-terse' the behaviour should be the same as it is
#     today.
#
#  BUGS
#     ??? 
#
#  SEE ALSO
#     ???/???
#*******************************

proc check_option_terse {} {
   global ts_config
   global default_args

   ts_log_fine "Testing qsub -terse option"
   set job_args "-o /dev/null -e /dev/null -terse $default_args"
   set output ""
   set not_expected [translate_macro MSG_QSUB_YOURJOBHASBEENSUBMITTED_SS "*" "*"]
   ts_log_fine "Running qsub -terse option: $job_args"
   set output [start_sge_bin "qsub" $job_args]
   # now check if the output contained "Your job ", which means the option is not
   # working
   # Do a second check to see if the job has been submitted, and $output has jobid
   if {$prg_exit_state == 0} {
      if {[string first $not_expected $output] == 0 && [get_extended_job_info $output] == 1} {
         ts_log_severe "not working:\n$output"
      } else {
         ts_log_fine "The -terse is working!!!!\n$output)"
      }
   } else {
      ts_log_severe "qsub $job_args failed:\n$output"
   }
}

#                                                             max. column:     |
#****** qsub/check_deadline() ******
# 
#  NAME
#     check_deadline -- ??? 
#
#  SYNOPSIS
#     check_deadline { } 
#
#  FUNCTION
#     ??? 
#
#  INPUTS
#
#  RESULT
#     ??? 
#
#  EXAMPLE
#     ??? 
#
#  NOTES
#     ??? 
#
#  BUGS
#     ??? 
#
#  SEE ALSO
#     ???/???
#*******************************
proc check_deadline {} {
   global ts_config
   global test_host job_output_dir

   if {$ts_config(product_type) == "sge"} {
      ts_log_config "not possible in sge system"
      return
   }

   # get command option string 
   set then [clock scan "1 minute"]
   set deadline [clock format $then -format "%Y%m%d%H%M.%S"]

   # deadline job (2 min) with deadline in 1 minutes
   set jobargs "-dl $deadline -l h=${test_host} -o $job_output_dir -e $job_output_dir $ts_config(product_root)/examples/jobs/sleeper.sh 120"
   # start job
   set job_id [submit_job $jobargs]
   if {$job_id < 0} {
      ts_log_severe "could not submit job"
      return
   }

   # now job is in queueing system (get timestamp now)
   ts_log_fine "monitoring job $job_id"

   set dlcontr_min 999999999
   set dlcontr_max 0
   while {[get_urgency_job_info ""] == 1 && $job_info(index) == $job_id} {
      set dlcontr $job_info($job_id,dlcontr)
      ts_log_fine "job has deadline $job_info($job_id,deadline), dlcontr $dlcontr"
      if {$dlcontr < $dlcontr_min} {
         set dlcontr_min $dlcontr
      }
      if {$dlcontr > $dlcontr_max} {
         set dlcontr_max $dlcontr
      }
      sleep 5
   }

   # now the job is done - delete job files
   set back [del_job_files $job_id $job_output_dir 2]
   if {$back != 2} {
      ts_log_severe "job $job_id did not produce error and output file"
   }

   # and now analyze the deadline policy info
   get_schedd_config schedd_config

   ts_log_fine "minimum dlcontr was $dlcontr_min"
   ts_log_fine "maximum dlcontr was $dlcontr_max"
   ts_log_fine "weight_deadline is  $schedd_config(weight_deadline)"

   if {$dlcontr_min != 0} {
      ts_log_severe "expected a minimum dlcontr of 0, but got $dlcontr_min"
   }
   if {$dlcontr_max != $schedd_config(weight_deadline)} {
      ts_log_severe "expected a maximum dlcontr of $schedd_config(weight_deadline), but got $dlcontr_max"
   }
}

