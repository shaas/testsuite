#!/vol2/TCL_TK/glinux/bin/expect
#___INFO__MARK_BEGIN__
##########################################################################
#
#  The Contents of this file are made available subject to the terms of
#  the Sun Industry Standards Source License Version 1.2
#
#  Sun Microsystems Inc., March, 2001
#
#
#  Sun Industry Standards Source License Version 1.2
#  =================================================
#  The contents of this file are subject to the Sun Industry Standards
#  Source License Version 1.2 (the "License"); You may not use this file
#  except in compliance with the License. You may obtain a copy of the
#  License at http://gridengine.sunsource.net/Gridengine_SISSL_license.html
#
#  Software provided under this License is provided on an "AS IS" basis,
#  WITHOUT WARRANTY OF ANY KIND, EITHER EXPRESSED OR IMPLIED, INCLUDING,
#  WITHOUT LIMITATION, WARRANTIES THAT THE SOFTWARE IS FREE OF DEFECTS,
#  MERCHANTABLE, FIT FOR A PARTICULAR PURPOSE, OR NON-INFRINGING.
#  See the License for the specific provisions governing your rights and
#  obligations concerning the Software.
#
#  The Initial Developer of the Original Code is: Sun Microsystems, Inc.
#
#  Copyright: 2001 by Sun Microsystems, Inc.
#
#  All Rights Reserved.
#
##########################################################################
#___INFO__MARK_END__

# Define the global veriables to give them back
global check_name check_description check_needs check_functions row
global check_category
global check_init_level_procedure
global check_root_access_needs

set check_root_access_needs "yes"
set check_init_level_procedure "sgepasswd_init_level"
set check_name         "sgepasswd"
set check_category     "COMPATIBILITY SYSTEM L10N VERIFIED"

set check_description(0)  "checking if sgepasswd file is correct"
set check_needs        "init_core_system"  ;# dependencies of this check (name of other check)

# setup and cleanup functions
set check_setup_function "sgepasswd_setup"
set check_cleanup_function "sgepasswd_cleanup"

# array with information about existing files/directories
global file_exists
set file_exists(sgepasswd) "0"
set file_exists(var_sgeCA) "0"
set file_exists(common_sgeCA) "0"

#timestamp use to create unique temporary files/directories
global actual_time
set actual_time [timestamp]

# define test's procedure order
set check_functions    "" 
lappend check_functions    "sgepasswd_6472859"




proc sgepasswd_init_level {} {
   global CHECK_ACT_LEVEL

   switch -- $CHECK_ACT_LEVEL {
      "0" {
         return 0
      }
   }

   return -1  ;# no other level else
}


# saving the existing keys and creating new ones in sgepasswd_setup() (and
# the corresponding restoring of keys in sgepasswd_cleanup()) was neccessary
# in a not well working environment - it shouldn't be neccessary in a good 
# working one and should be replaced by dependency to the install_core_test 
# then.

proc sgepasswd_setup {} {
   global ts_config
   global CHECK_DEBUG_LEVEL CHECK_ACTUAL_TEST_PATH 
   global log_user
   global file_exists 
   global actual_time
   

   if { $ts_config(product_feature) == "csp" } {
      return
   }

   #switch on output of called programs for debug
   if { $CHECK_DEBUG_LEVEL != 0 } {
       set log_user 1
   }

   #check if sgepasswd file is present and save it
   set exist [glob -nocomplain $ts_config(product_root)/$ts_config(cell)/common/sgepasswd]
   if {$exist != {} } {
      set file_exists(sgepasswd) "1"
      #save existing file
      set id [start_remote_prog $ts_config(master_host) "root" "mv" "-f  $ts_config(product_root)/$ts_config(cell)/common/sgepasswd /tmp/sgepasswd_$actual_time"]
   }

   #check if there is cerificate directory for this installation and save it
   set exist [glob -nocomplain -type d /var/sgeCA/$ts_config(commd_port)]
   if {$exist != {} } {
      set file_exists(var_sgeCA) "1"
      #save existing directory
      set id [start_remote_prog $ts_config(master_host) "root" "mv" "-f /var/sgeCA/$ts_config(commd_port) /tmp/sgeCA_$ts_config(commd_port)_$actual_time"]
   }

   set exist [glob -nocomplain -type d $ts_config(product_root)/$ts_config(cell)/common/sgeCA]
   if {$exist != {} } {
      set file_exists(common_sgeCA) "1"
      #save existing directory
      set id [start_remote_prog $ts_config(master_host) "root" "mv" "-f  $ts_config(product_root)/$ts_config(cell)/common/sgeCA /tmp/GE_sgeCA_$actual_time"]
   }


   #create new certificates for testing SGEPASSWD
   set id [open_remote_spawn_process $ts_config(master_host) "root" "$ts_config(product_root)/util/sgeCA/sge_ca" "-init"]
   set sp_id [lindex $id 1]
   set timeout 10
      expect {
         -i $sp_id "*Hit <RETURN>" {
            ts_send $sp_id "\n"
            set timeout 10 
            exp_continue
            }
         #entering some test values
         -i $sp_id  "*Please enter" {
            ts_send $sp_id "tt\n"
            set timeout 10 
            exp_continue
            }
         -i $sp_id  "*Do you want to use these data " {
            ts_send $sp_id "\n"
            set timeout 10 
            exp_continue
            }
         -i $sp_id  "*Do you want to recreate " {
            ts_send $sp_id "\n"
            set timeout 10
            exp_continue
            }
         -i $sp_id  eof {
            close_spawn_process $id
            }
         -i $sp_id  timeout {
            close_spawn_process $id
            }
      }
   ts_log_fine "Check_setup function finished"
   close_spawn_process $id
}

proc sgepasswd_cleanup {} {

   global ts_config
   global CHECK_DEBUG_LEVEL CHECK_ACTUAL_TEST_PATH CHECK_USER
   global log_user
   global file_exists
   global actual_time


   #  TODO (CR):
   #  This test does not work for csp systems, because the certificates not valid anymore
   if { $ts_config(product_feature) == "csp" } {
      return
   }


   #clean up certificate directories
   set id [start_remote_prog $ts_config(master_host) "root" "rm" "-rf  /var/sgeCA/$ts_config(commd_port)"]
   set id [start_remote_prog $ts_config(master_host) "root" "rm" "-rf  $ts_config(product_root)/$ts_config(cell)/common/sgeCA"]

   #restore saved directories
   if { $file_exists(var_sgeCA) !=  "0" } {
      #restore /var/sgeCA directory
      set id [start_remote_prog $ts_config(master_host) "root" "mv" "-f  /tmp/sgeCA_$ts_config(commd_port)_$actual_time /var/sgeCA"]
      # TODO (CR): 
      # use get_tmp_directory_name for temporary directories and
      # get_tmp_file_name for temporary files

      # TODO (CR):
      # when moving this files be sure that start_remote_prog was successful by checking
      # $prg_exit_state (returned by start_remote_prog) 
      
   }
   if { $file_exists(common_sgeCA) !=  "0" } {
      #restore $SGE_ROOT/$(cell)/common/sgeCA directory
      set id [start_remote_prog $ts_config(master_host) "root" "mv" "-f  /tmp/GE_sgeCA_$actual_time $ts_config(product_root)/$ts_config(cell)/common/sgeCA"]
   }


   #clean up sgepasswd file
   set id [start_remote_prog $ts_config(master_host) "root" "rm" "-rf  $ts_config(product_root)/$ts_config(cell)/common/sgepasswd"]

   if { $file_exists(sgepasswd) !=  "0" } {
   #restore sgepasswd file
   set id [start_remote_prog $ts_config(master_host) "root" "mv" "-f  /tmp/sgepasswd_$actual_time $ts_config(product_root)/$ts_config(cell)/common/sgepasswd"]
   }

   #switch off debug mode 
   if { $CHECK_DEBUG_LEVEL != 0 } {
      set log_user 0 
   }

   ts_log_fine "Clean up function finished"
}

#                                                             max. column:     |
#****** sgepasswd_6472859() ******
# 
#  NAME
#     sgepasswd_6472859
#
#     Tests the sgepasswd call with corrupted SGEPASSWD file which causes 
#     a segmentation fault in N1GE 6.0.
#
#  SYNOPSIS
#     sgepasswd_6472859 {} 
#
#  FUNCTION
#     sgepasswd_6472859 
#
#  NOTES
#     Tests the sgepasswd call with corrupted SGEPASSWD file which causes 
#     a segmentation fault in N1GE 6.0. 
#
#  BUGS
#       CR 6472859 
#
#*******************************
proc sgepasswd_6472859 {} {
   global ts_config
   global CHECK_ACTUAL_TEST_PATH 


   if { $ts_config(product_feature) == "csp" } {
      ts_log_config "can't run this test for CSP systems"
      return
   }


   # TODO (CR):
   # don't use subdiretories just for test setup files!
   # subdiretories should be used only for further tests in the checktree
   # -> store the *.test files in the local test directory!

   #read sgepasswd filenames that will be used in test
   set files [glob -nocomplain $CHECK_ACTUAL_TEST_PATH/test_files/*.test]
   if { $files == {} } {
      #if no files are present for test, test ends
   }

   foreach filename $files {
      #clean sgepasswd file before copying new test file
      set id [start_remote_prog $ts_config(master_host) "root" "rm" "-f  $ts_config(product_root)/$ts_config(cell)/common/sgepasswd"]
      
      #copy new test file
      set id [start_remote_prog $ts_config(master_host) "root" "cp" "-f  $filename $ts_config(product_root)/$ts_config(cell)/common/sgepasswd"]

      #get only name of file for notifications 
      set name [file tail $filename]
      ts_log_fine "Testing file: $name ..."
 
      #run sgepasswd program and test it
      set master_arch [resolve_arch $ts_config(master_host)]
      set id [open_remote_spawn_process $ts_config(master_host) "root" "$ts_config(product_root)/bin/$master_arch/sgepasswd" "root"]
      set sp_id [lindex $id 1]
      set timeout 10

      #process sgepasswd program
      expect {
         -i $sp_id "New password" {
            ts_send $sp_id "test\n"
            set timeout 10 
            exp_continue
            }
         -i $sp_id  "Re-enter new password" {
            ts_send $sp_id "test\n"
            set timeout 10 
            exp_continue
            } 
         -i $sp_id  "Password changed" { 
            ts_log_fine "Password changed for file: $name"
            close_spawn_process $id 
            }
         -i $sp_id  "sgepasswd: sgepasswd file corrupted" {
            ts_log_fine "Password not changed (corrupted file) for file: $name" 
            close_spawn_process $id
            }
          #Segmentation fault should not appear if test is going to pass
         -i $sp_id  "*Segmentation Fault" {
            close_spawn_process $id
            ts_log_severe "Test failed - Segmentation fault with test file $name " 
            }
         -i $sp_id  eof {
            ts_log_fine "expect eof"
            close_spawn_process $id
            }
         -i $sp_id  timeout {
            ts_log_fine "expect timeout"
            close_spawn_process $id
            }
      }
   ts_log_fine "Test file: $name completed"
   close_spawn_process $id

   #close foreach
   }
   ts_log_fine "Test finished successfully"
}
